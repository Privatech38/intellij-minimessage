{
    parserClass="com.github.privatech.minimessage.parser.MiniMessageParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="MiniMessage"
    psiImplClassSuffix="Impl"
    psiPackage="com.github.privatech.minimessage.psi"
    psiImplPackage="com.github.privatech.minimessage.psi.impl"

    elementTypeHolderClass="com.github.privatech.minimessage.psi.MiniMessageTypes"
    elementTypeClass="com.github.privatech.minimessage.psi.MiniMessageElementType"
    tokenTypeClass="com.github.privatech.minimessage.psi.MiniMessageTokenType"

    psiImplUtilClass="com.github.privatech.minimessage.psi.impl.MiniMessagePsiImplUtil"

    tokens=[
        COLON=":"
        LT="<"
        GT=">"
        SLASH="/"
        ESCAPE='\'

        // Custom tokens
        PLAIN_TEXT
        TAG_NAME
        CUSTOM_TAG_NAME
        ARGUMENT
        STRING
        LEGACY_FORMATTING_CODE
        WHITE_SPACE
    ]
}

root ::= content

private content ::= (tag | LEGACY_FORMATTING_CODE | PLAIN_TEXT) *

private tag ::= opening_tag | closing_tag

opening_tag    ::= LT tag_name tag_argument * SLASH? GT // Can be self-closing, made like this for simplicity
closing_tag    ::= LT SLASH tag_name GT { pin=2 }

private tag_name ::= TAG_NAME | CUSTOM_TAG_NAME

tag_argument ::= COLON argument_type { pin=1 methods=[toString getTextRange toAdventureArgument] }
private argument_type ::= STRING | ARGUMENT
