//noinspection BnfSuspiciousTokenForFile
{
    parserClass="com.github.privatech.minimessage.parser.MiniMessageParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="MiniMessage"
    psiImplClassSuffix="Impl"
    psiPackage="com.github.privatech.minimessage.psi"
    psiImplPackage="com.github.privatech.minimessage.psi.impl"

    elementTypeHolderClass="com.github.privatech.minimessage.psi.MiniMessageTypes"
    elementTypeClass="com.github.privatech.minimessage.psi.MiniMessageElementType"
    tokenTypeClass="com.github.privatech.minimessage.psi.MiniMessageTokenType"

    tokens=[
        COLON=":"
        LT="<"
        GT=">"
        SLASH="/"
        ESCAPE='\'
        SECTION="ยง"

        // Custom tokens
        PLAIN_TEXT
        TAG_NAME
        ARGUMENT
        STRING
        LEGACY_COLOR_CODE
        WHITE_SPACE
    ]
}

root ::= content

content ::= (possibleTag | legacyFormattingCode | PLAIN_TEXT) *

private possibleTag ::= ESCAPE LT? | emptyTag | LT+ WHITE_SPACE | tag

tag ::= autoClosedTag | tagOpening content tagClosing?

emptyTag      ::= LT+ GT
autoClosedTag ::= LT+ TAG_NAME tagArgument? SLASH GT
tagOpening    ::= LT+ TAG_NAME tagArgument? GT
tagClosing    ::= LT+ SLASH TAG_NAME GT {pin=2}

tagArgument ::= COLON argumentType tagArgument? {pin=1}
argumentType ::= STRING | ARGUMENT

legacyFormattingCode ::= !ESCAPE SECTION LEGACY_COLOR_CODE
